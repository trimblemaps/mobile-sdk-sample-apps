// Top-level build file where you can add configuration options common to all sub-projects/modules.
def REACT_NATIVE_VERSION = new File(['node', '--print',"JSON.parse(require('fs').readFileSync(require.resolve('react_native/package.json'), 'utf-8')).version"].execute(null, rootDir).text.trim())

ext {
    buildToolsVersion = "33.0.0"
    minSdkVersion = 26
    compileSdkVersion = 35
    targetSdkVersion = 35
    ndkVersion = "27.1.12297006"
    kotlinVersion = "2.1.0"
}

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.6.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.0")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url = 'https://plugins.gradle.org/m2' }
        maven { url 'https://trimblemaps.jfrog.io/artifactory/android' }
    }

    buildscript {
        repositories {
            mavenLocal()
            google()
            mavenCentral()
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                if (project.android.hasProperty("defaultConfig")) {
                    project.android.defaultConfig {
                        if (project.android.defaultConfig.hasProperty("externalNativeBuild")) {
                            externalNativeBuild {
                                cmake {
                                    arguments "-DCMAKE_OBJECT_PATH_MAX=1024"
                                }
                            }
                        }
                    }
                }

                if (project.android.hasProperty("externalNativeBuild")) {
                    // Use system temp directory with unique workspace path to avoid permission issues
                    def tempDir = System.getProperty("java.io.tmpdir")
                    def workspaceHash = rootProject.projectDir.absolutePath.hashCode().toString().replaceAll("-", "")
                    def shortBuildDir = new File("${tempDir}/rn-build/${workspaceHash}/${project.name}")
                    if (!shortBuildDir.exists()) {
                        shortBuildDir.mkdirs()
                    }
                    project.android.externalNativeBuild {
                        cmake {
                            buildStagingDirectory = shortBuildDir
                        }
                    }
                }
            }
        }
    }
}

