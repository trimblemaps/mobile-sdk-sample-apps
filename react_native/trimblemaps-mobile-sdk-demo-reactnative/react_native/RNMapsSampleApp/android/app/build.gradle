plugins {
    id 'com.android.application'
    id 'com.facebook.react'
}

// Ensure autolinking directories exist before configuration
def autolinkingDir = file("${buildDir}/generated/autolinking/src/main/java")
if (!autolinkingDir.exists()) {
    autolinkingDir.mkdirs()
}

/**
 * React Native Android app configuration
 * see ref: https://reactnative.dev/docs/react-native-gradle-plugin
 */
react {
    autolinkLibrariesWithApp()
}

apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false

preBuild.doFirst {
    ant.replaceregexp(match:'android.support.v4.util.Pools;', replace:'androidx.core.util.Pools;', flags:'g', byline:true) {
        fileset(dir: '../../node_modules/react-native-gesture-handler/android/src/main/java/com/swmansion/gesturehandler/react/', includes: '*.java')
    }
}
def jscFlavor = 'org.webkit:android-jsc:+'

android {
    ndkVersion rootProject.ext.ndkVersion
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    namespace "com.mapssampleapp"

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/jniLibs']
        }
    }

    defaultConfig {
        applicationId "com.mapssampleapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        externalNativeBuild {
            cmake {
                arguments "-DCMAKE_OBJECT_PATH_MAX=512",
                          "-DCMAKE_VERBOSE_MAKEFILE=ON",
                          "-DANDROID_STL=c++_shared"
            }
        }
    }
    signingConfigs {
        release {
            keyAlias System.getenv("JENKINS_ANDROID_KEY_CREDS_USR")
            keyPassword System.getenv("JENKINS_ANDROID_KEY_CREDS_PSW")
            storeFile file(System.getenv("JENKINS_ANDROID_KEYSTORE_PATH") ?: "/")
            storePassword System.getenv("JENKINS_ANDROID_KEYSTORE_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

repositories {
    mavenLocal()
    google()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2' }
    maven { url 'https://trimblemaps.jfrog.io/artifactory/android' }
}

dependencies {
    implementation "com.facebook.react:react-android"
    implementation "com.facebook.react:hermes-android"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.2'
    implementation 'com.trimblemaps.reactnative:maps-reactnative-sdk:0.3.0'
}